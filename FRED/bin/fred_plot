#!/usr/bin/perl

##########################################################################################
## 
##  This file is part of the FRED system.
##
## Copyright (c) 2010-2012, University of Pittsburgh, John Grefenstette, Shawn Brown, 
## Roni Rosenfield, Alona Fyshe, David Galloway, Nathan Stone, Jay DePasse, 
## Anuroop Sriram, and Donald Burke
## All rights reserved.
##
## Copyright (c) 2013-2021, University of Pittsburgh, John Grefenstette, Robert Frankeny,
## David Galloway, Mary Krauland, Michael Lann, David Sinclair, and Donald Burke
## All rights reserved.
##
## FRED is distributed on the condition that users fully understand and agree to all terms of the 
## End User License Agreement.
##
## FRED is intended FOR NON-COMMERCIAL, EDUCATIONAL OR RESEARCH PURPOSES ONLY.
##
## See the file "LICENSE" for more information.
##
##########################################################################################

use strict;
use warnings;
use Env;
use Getopt::Long qw(:config no_ignore_case bundling);
use POSIX qw(strftime);

my $FRED = $ENV{FRED_HOME};
die "$0: Please set environmental variable FRED_HOME to location of FRED home directory\n" if not $FRED;

my $FREDRESULTS = $ENV{FRED_RESULTS};
$FREDRESULTS = "$ENV{FRED_HOME}/RESULTS" if not $FREDRESULTS;

my $datestring = strftime "%y-%b-%d %H:%M", localtime;
chomp $datestring;
my $version = `$FRED/bin/fred_version`;
chomp $version;
my $versiondate = "FRED $version $datestring";

my $gnuplot = $ENV{FRED_GNUPLOT};
die "$0: no gnuplot\n" if (not $gnuplot or (not -x $gnuplot));

my $tmp = "tmp$$";

my $cmd = "gnuplot -e 'set print \"$tmp\"; print GPVAL_TERMINALS'";
# print "cmd = |$cmd|\n";
system($cmd);
my $terminals = `cat $tmp`;
chomp $terminals;
my $pdfcairo = $terminals =~ /pdfcairo/;
# print "terminals |$terminals|\npdfcairo = |$pdfcairo|\n";
system("unlink $tmp");

my $commandline = "fred_plot @ARGV";
my @data = ();

my @vars = ();
my @labels = ();
my @keys = ();
my @colors = ();
my $xmin = "0";
my $xmax = "*";
my $ymin = "0";
my $ymax = "*";
my $display = 1;
my $errorbars;
my $title = "FRED Simulation";
my $all;
my $font = "Helvetica";
my $fontsize = "12";
if ($pdfcairo) {
  $fontsize = "18";
}
my $boxes;
my $normalize;
my $scale = 100000;
my $legend = "";
my $linewidth = 0;
my $xlabel = "";
my $ylabel = "Individuals";
my $xtics = 1;
my $mxtics = 10;
my $outfile = "";
my $help = "";
my $show_year = 0;
my $show_vars = 0;
my $run = 0;
my $make_png = 0;
my $use_mean_std = 0;
my $key_out = 0;
my $logscale = 0;
my $weeklycount = 0;
my $datecol = 0;
my $daily = 0;
my $weekly = 0;
my $biweekly = 0;
my $monthly = 0;
my $yearly = 0;
my $externalfile = "";
my $EX = 0;
my $EY = 1;
my $ELAB = "";
my $opt_result = GetOptions(
			    "a" => \$all,
			    "all" => \$all,
			    "b" => \$boxes,
			    "bars" => \$boxes,
			    "boxes" => \$boxes,
			    "c=s" => \@colors,
			    "colors=s" => \@colors,
			    "histogram" => \$boxes,
			    "h" => \$help,
			    "help" => \$help,
			    "d=i" => \$display,
			    "display=i" => \$display,
			    "D" => \$daily,
			    "W" => \$weekly,
			    "B" => \$biweekly,
			    "M" => \$monthly,
			    "A" => \$yearly,
			    "weekly" => \$weeklycount,
			    "monthly" => \$monthly,
			    "yearly" => \$yearly,
			    "e" => \$errorbars,
			    "f=s" => \$font,
			    "k=s" => \@keys,
			    "lw=f" => \$linewidth,
			    "l=s" => \@labels,
			    "labels=s" => \@labels,
			    "Log=i" => \$logscale,
			    "T=s" => \$title,
			    "t=s" => \$title,
			    "v=s" => \@vars,
			    "var=s" => \@vars,
			    "V" => \$show_vars,
			    "x=f" => \$xmin,
			    "X=f" => \$xmax,
			    "y=f" => \$ymin,
			    "Y=f" => \$ymax,
			    "errorbars" => \$errorbars,
			    "font=s" => \$font,
			    "F=f" => \$fontsize,
			    "fsize=f" => \$fontsize,
			    "keys=s" => \@keys,
			    "key_out" => \$key_out,
			    "legend=s" => \$legend,
			    "mean" => \$use_mean_std,
			    "n" => \$normalize,
			    "normalize" => \$normalize,
			    "r=i" => \$run,
			    "s=f" => \$scale,
			    "scale=f" => \$scale,
			    "title=s" => \$title,
			    "w" => \$weeklycount,
			    "xmin=i" => \$xmin,
			    "xmax=i" => \$xmax,
			    "Xmax=i" => \$xmax,
			    "ymin=i" => \$ymin,
			    "ymax=i" => \$ymax,
			    "Ymax=i" => \$ymax,
			    "ylabel=s" => \$ylabel,
			    "xlabel=s" => \$xlabel,
			    "xtics=i" => \$xtics,
			    "year" => \$show_year,
			    "o=s" => \$outfile,
			    "output=s" => \$outfile,
			    "external=s" => \$externalfile,
			    "EX=i" => \$EX,
			    "EY=i" => \$EY,
			    "ELAB=s" => \$ELAB,
			    "png" => \$make_png,
			   );

my $usage = <<EOH;
usage: fred_plot -k key -v var [ options ], where options include:
       -a: show all individual runs.
       -b: plot bar charts instead of curves.
       -d 0/1 : display the image file (default = 1).
       -e: include errorbars on plot.
       -f font : use the named font.
       -F fontsize: specify font size.
       -h: print this help message.
       -k key [ -k key ... ]: keys of jobs to plot.
       -l label [ -l label ... ]: labels to use for each variable.
       --lw <n>: set linewidth to <n>
       -n: scale y-axis to show counts per <s> people.
       -o outfile: send output image to indicated file.
       -r <n>: show individual run <n>.
       -s <s> : scale factor for normalization.
       -t title: Title to appear on plot.
       -v var [ -v var ... ]: variable to be plotted.
       -w: plot variables averaged over epi weeks.
       -x xmin: min value for x-axis.
       -X xmax: max value for x-axis.
       -y ymin: min value for y-axis.
       -Y ymax: max value for y-axis.
       -A: show annual date labels.
       -M: show monthly date labels.
       -W: show weekly date labels.
       -M: show bi-weekly date labels.
       -V: print list of available variables and exit.
       --year: include years in date labels.
       --xtics <n>: put xtics every <n> units.
       --xlabel <s>: label the x axis with "s".
       --ylabel <s>: label the y axis with "s".
       --legend <s>: set legend control, e.g. "out"
       --png 0/1: output to png rather than pdf (default = 0).
EOH

die $usage if $help;
die $usage if not @keys;

# resolve conflicting options

# the following permit these options to have multiple values separated
# by commas e.g. -k X,Y,Z is the same as -k X -k Y -k Z
@keys = split(/,/, join(',',@keys));
# print  "$0: keys = ";
# print "|$_|\n" for @keys;
# exit;

# check keys
my $bindir = "$FRED/bin";
my %id_of_key = ();
my $idstr = "";
for my $key (@keys) {
  my $id = `$bindir/fred_id $key`;
  chomp $id;
  die "$0: UNKNOWN key: $key\n" if $id eq "UNKNOWN";
  $idstr.= "-$id";

  my $status = `$bindir/fred_status -k $key`;
  chomp $status;
  $status =~ s/\s.*//;
  die "$0 bad status: $status\n" if $status ne "FINISHED";
  $id_of_key{$key} = $id;

  if ($show_vars) {
    my $available = `cat $FREDRESULTS/JOB/$id/OUT/PLOT/VARS`;
    print "$0: Plot variables:\n$available\n";
    exit;
  }
}

die $usage if not @vars;

# the following permit these options to have multiple values separated
# by commas e.g. -v X,Y,Z is the same as -v X -v Y -v Z
@vars = split(/,/, join(',',@vars));
@labels = split(/,/, join(',',@labels));
@colors = split(/,/, join(',',@colors));

if (not @colors) {
  @colors = qw/blue red green orange purple black skyblue greenyellow medium-blue dark-green dark-blue/;
}

# create an array of labels for variables to appear in the plot key
my @label = ();
for my $i (0..$#vars) {
  if ($i < scalar @labels) {
    $label[$i] = $labels[$i];
  } else {
    $label[$i] = $vars[$i];
  }
}

# create a good name for the plt and pdf files
my $keystr = join "-", @keys;
my $varstr = join "-", @vars;
$varstr =~ s/\//-divided_by-/g;
my $plotfile = "plot-$keystr-$varstr.plt";
my $pdffile = "";
if (not $outfile) {
  $outfile = "plot-$keystr-$varstr.pdf";
  $pdffile = "plot-$keystr-$varstr.pdf";
  if ($make_png) {
    $outfile = "plot-$keystr-$varstr.png";
  }
} else {
  $plotfile = "plot-$outfile.plt";
  $pdffile = "$outfile.pdf";
  if ($make_png) {
    $outfile = "$outfile.png";
  } else {
    $outfile = "$outfile.pdf";
  }
}

## if ($make_png) {
  ## $font = "Arial";
  ## $fontsize = $fontsize - 2;
## } else {
  ## $title =~ s/_/\\\\_/g;
## }
$title =~ s/_/\\\\_/g;

  
# get the data files associated with each variable

my $nkeys = scalar @keys;
for my $i (0..$#vars) {
  my $v = $vars[$i];
  my $key = $keys[$i % $nkeys];
  # print "key = |$key| v = |$v|\n";
  my $runs = get_runs($key);
  $run = 0 if ($runs < $run);
  get_data($key, $v, 0) if ($all or not $run);
  if ($all) {
    for my $run_number (1..$runs) {
      get_data($key, $v, $run_number);
    }
  } elsif ($run) {
    get_data($key, $v, $run);
  }
}

# create the input file for gnuplot
my $date = scalar localtime;
my $titlefontsize = $fontsize + 4;
my $terminal = "pdf font '$font,$fontsize' linewidth 3 size 7,5";
if ($pdfcairo) {
  $terminal = "pdfcairo font '$font,$fontsize' linewidth 3 size 7,5";
}
if ($make_png == 1) {
  # $terminal = "pngcairo font '$font,$fontsize' linewidth 1";
}

my $setkey = "set key at graph 0.96, graph 0.96 right";
$setkey = "set key $legend" if $legend;
if ($key_out) {
  $setkey = "set key outside right";
}

my $ticfontsize = $fontsize-1;

my $showdate = 0;
$showdate = 1 if $daily or $weekly or $biweekly or $monthly or $yearly;
$showdate = 0 if $weeklycount;

$xtics = 1 if $xtics < 1;

# line width
my $lw = "lw 4";
if ($pdfcairo) {
  $lw = "lw 2";
}
if ($linewidth) {
  $lw = "lw $linewidth";
}

open FH, ">$plotfile";
print FH <<"EOF";
#!$gnuplot
#
# file: $plotfile
# created: $date
#
# command line used to generate this file:
# $commandline
#
set terminal $terminal
set title \"$title\" font \"$font,$titlefontsize\"
set output \"$outfile\"
set output \"$pdffile\"
$setkey
set xtics nomirror out $xtics font "$font,$ticfontsize"
set ytics mirror in font "$font,$ticfontsize"
# set label sprintf("\%s","$versiondate") at graph -0.1,-0.1 left font \'$font,6\'
EOF

if ($logscale) {
  print FH "set logscale y $logscale\n";
}

if ($normalize) {
  if ($scale == 1) {
    $ylabel = "Fraction of Population";
  } elsif ($scale == 100) {
    $ylabel = "Percent";
  } else {
    $ylabel = "Per $scale";
  }
}

if ($xlabel eq "") {
  $xlabel = "Date" if $showdate;
  $xlabel = "Week" if $weekly;
  $xlabel = "Month" if $monthly;
  $xlabel = "Year" if $yearly;
  $xlabel = "Week" if $weeklycount;
}


# define format for date-based xtics
my $dateformat = 'date_format(n,x) = (n % ' . "$xtics" . '==0) ? x : "" ';

if ($weekly) {
  if ($show_year) {
    $dateformat = 'date_format(n,x) = (n % ' . "$xtics" . '==0) && (substr(x,10,11) eq "01" || substr(x,10,11) eq "08" || substr(x,10,11) eq "15" || substr(x,10,11) eq "22") ? x : "" ';
  } else {
    $dateformat = 'date_format(n,x) = (n % ' . "$xtics" . '==0) && (substr(x,10,11) eq "01" || substr(x,10,11) eq "08" || substr(x,10,11) eq "15" || substr(x,10,11) eq "22") ? substr(x,6,11) : "" ';
  }
}

if ($biweekly) {
  if ($show_year) {
    $dateformat = 'date_format(n,x) = (substr(x,10,11) eq "01" || substr(x,10,11) eq "15") ? x : "" ';
  } else {
    $dateformat = 'date_format(n,x) = (substr(x,10,11) eq "01" || substr(x,10,11) eq "15") ? substr(x,6,11) : "" ';
  }
}

if ($monthly) {
  if ($show_year) {
    $dateformat = 'date_format(n,x) = (n % ' . "$xtics" . '==0) && (substr(x,10,11) eq "01") ? x : "" ';
  } else {
    $dateformat = 'date_format(n,x) = (n % ' . "$xtics" . '==0) && (substr(x,10,11) eq "01") ? substr(x,6,11) : "" ';
  }
}

if ($yearly) {
  if ($show_year) {
    $dateformat = 'date_format(n,x) = (n % ' . "$xtics" . '==0) && (substr(x,6,11) eq "Jan-01") ? substr(x,1,4) : "" ';
  } else {
    $dateformat = 'date_format(n,x) = (n % ' . "$xtics" . '==0) && (substr(x,6,11) eq "Jan-01") ? x : "" ';
  }
}


if ($weeklycount) {
  if ($show_year) {
    if ($xtics == 1) {
      $dateformat = 'date_format(n,x) = x';
    } else {
      $dateformat = 'date_format(n,x) = ((x[6:*]+0)%' . "$xtics" . '==0) && (x[6:*]+0 != 53) ? x : "" ';
    }
  } else {
    if ($xtics == 1) {
      $dateformat = 'date_format(n,x) = sprintf("%s",x[6:*]) ';
    } else {
      $dateformat = 'date_format(n,x) = ((x[6:*]+0)%' . "$xtics" . '==0) && (x[6:*]+0 != 53) ? sprintf("%s",x[6:*]) : "" ';
    }
  }
}

if ($showdate) {
  print FH "$dateformat\n";
  print FH "set xtics rotate by 270\n";
  print FH "set grid ytics\n";
} elsif ($weeklycount) {
  print FH "$dateformat\n";
  print FH "set xtics rotate by 270\n";
  print FH "set grid xtics ytics\n";
} else {
  print FH "set grid xtics ytics\n";
}

print FH "set xrange [$xmin:$xmax]\n";
print FH "set yrange [$ymin:$ymax]\n";
print FH "set datafile separator ','\n";
print FH "set xlabel \"$xlabel\" offset 0,0.5\n";
print FH "set ylabel \"$ylabel\" offset 1.2,0\n";
print FH "set bars 2.0 front\n";

if ($boxes) {
  my $vn = scalar @vars;
  print FH "\n# set up boxes\n";
  print FH "vars = $vn\n";
  print FH "fredkeys = ", scalar @keys, "\n";
  print FH "nplots = vars\n";
  print FH "total_relative_boxwidths = 0.8\n";
  print FH "dx = total_relative_boxwidths/(1.0*nplots)\n";
  print FH "set boxwid (total_relative_boxwidths/(1.0*nplots)) relative\n";
  print FH "set style fill solid 1.0 border 0\n";
  print FH "get_shift(n) = dx*(n-0.5*nplots-0.5)\n";
  # print FH "set xtics offset graph (-1.0*get_shift(1)), 0\n";
  print FH "\n";
}

if ($showdate) {
  print FH "set xtics rotate by 270\n";
  print FH "\n";
}

my $n = 0;
my $ncolor = 0;
for my $i (0..$#vars) {
  my $v = $vars[$i];
  my $titlevar = $v;
  my $vlabel = $label[$i];
  my $key = $keys[$i % $nkeys];
  my $runs = get_runs($key);
  $run = 0 if ($runs < $run);
  my $ptitle = "$vlabel";
  my $plotcmd = ($n == 0);
  $n++;
  if ($plotcmd) {
    if ($externalfile) {
      print FH "plot '$externalfile' using $EX:$EY title '$ELAB' with lines lt rgb 'black' $lw, \\\n";
    } else {
      print FH "plot \\\n";
    }
  }
  $ptitle =~ s/_/\\_/g;  
  print FH plot_command($ptitle,$n,0) if ($all or not $run);
  $plotcmd = 0;
  if ($all) {
    for my $run_number (1..$runs) {
      print FH plot_command("Run $run_number",$n,$run_number);
    }
  } elsif ($run) {
    print FH plot_command("",$n,$run);
  }
}
print FH "\n";
print FH "$_\n" for @data;
print FH "\n";
close FH;
# system "chmod +x $plotfile";
system ("gnuplot " . $plotfile);
print "fred_plot: source_file = $plotfile  image_file = $outfile\n";
if ($make_png) {
  system "convert -density 300 $pdffile -background white -quality 100 $outfile";
}
system ("open $outfile") if $display;
exit;

##########################################################

sub get_data {
  my ($key, $var, $run) = @_;
  # print "get_data key = $key var = |$var| run = |$run|\n";
  my $tmpfile = ".tmpfile";

  my $numerator = "";
  my $denominator = "";
  my $summand = "";
  if ($var =~ '/') {
    ($numerator, $denominator) = $var =~ /(.*)\/(.*)/;
    die "$0: bad variable name |$var|\n" if (not $numerator or not $denominator);
    $var = $numerator;
  } else {
    if ($var =~ '\+') {
      ($numerator, $summand) = $var =~ /(.*)\+(.*)/;
      die "$0: bad variable name |$var|\n" if (not $numerator or not $summand);
      $var = $numerator;
    }
  }

  my $time = "DAILY";
  $time = "WEEKLY" if $weeklycount;
  my $file = $var;
  # print "file base = |$file|\n";
  
  # find the CSV files directory
  my $id = `fred_id $key`;
  chomp $id;
  my $csvfile = "$FREDRESULTS/JOB/$id/OUT/PLOT/$time/$file.csv";
  if (not -e $csvfile) {
    my $available = `cat $FREDRESULTS/JOB/$id/OUT/PLOT/VARS`;
    die "$0: Can't find variable $file in FRED job $key with id $id\nSelect variable from list:\n$available\n";
  }
  # print "csvfile = |$csvfile|\n";

  my $csvfile2 = "";
  if ($denominator) {
    my $file2 = $denominator;
    # print "div file base = |$file2|\n";
    
    # find the CSV files directory
    $csvfile2 = "$FREDRESULTS/JOB/$id/OUT/PLOT/$time/$file2.csv";
    if (not -e $csvfile2) {
      my $available = `cat $FREDRESULTS/JOB/$id/OUT/PLOT/VARS`;
      die "$0: Can't find variable $file2 in FRED job $key with id $id\nSelect variable from list:\n$available\n";
    }
  }
  if ($summand) {
    my $file2 = $summand;
    # print "sum file base = |$file2|\n";
    
    # find the CSV files directory
    $csvfile2 = "$FREDRESULTS/JOB/$id/OUT/PLOT/$time/$file2.csv";
    if (not -e $csvfile2) {
      my $available = `cat $FREDRESULTS/JOB/$id/OUT/PLOT/VARS`;
      die "$0: Can't find variable $file2 in FRED job $key with id $id\nSelect variable from list:\n$available\n";
    }
  }

  my @a = ();
  open IN, $csvfile;
  while (my $line = <IN>) {
    chomp $line;
    push @a, $line;
  }
  close IN;

  my @b = ();
  if ($csvfile2) {
    open IN, $csvfile2;
    while (my $line = <IN>) {
      chomp $line;
      push @b, $line;
    }
    close IN;
  }

  my @tmpdata = ();
  if (@b) {

    # add or divide elements in each line in @a by corresponding line in @b
    for my $aline (@a) {
      my $bline = shift @b;
      my @x = split(',',$aline);
      my @y = split(',',$bline);
      my @z = ();
      if ($x[0] eq "INDEX") {
	      @z = @x;
      } else {
	      # index
	      push @z, $x[0];
        # number of runs
        push @z, $x[1];
        # popsize
        push @z, $x[2];
        for my $i (3..(9+$x[1])) {
          if ($denominator) {
            # NOTE: division by zero is defined as 0 here!
            my $quot = 0;
            $quot = $x[$i] if $x[$i] == 0;
            $quot = $x[$i]/$y[$i] if $y[$i] != 0;
            push @z, $quot;
          } else {
	          my $sum = $x[$i] + $y[$i];
	          push @z, $sum;
	        }
	      }
      }
      my $zline = join(",",@z);
      push @tmpdata, $zline;
    }
  } else {
    @tmpdata = @a;
  }

  my $datefile = "";
  my $r = 1;
  my $found = 0;
  while (not $found) {
    $datefile = "$FREDRESULTS/JOB/$id/OUT/RUN$r/DAILY/Date.txt";
    # print "datefile |$datefile|\n"; system "ls $datefile";
    $found = (-e $datefile);
    $r++;
  }
  # print "datefile found |$datefile|\n";
  # $tmpdata[0] .= ",DATE";
  $tmpdata[0] = "DATE,$tmpdata[0]";
  my @mon = qw/Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec/;
  if ($found) {
    my $first = 1;
    open DATE,$datefile or die "Can't find datefile $datefile\n";
    my $i = 0;
    while (my $line = <DATE>) {
      # print "$line";
      chomp $line;
      my ($d, $str) = split " ", $line;
      my ($yr, $m, $day) = split '\-', $str;
      $str = "$yr-$mon[$m-1]-$day";
      if ($weeklycount) {
	      # $tmpdata[($d/7)+1] .= ",$str" if ($i % 7 eq 0);
	      my $j = $d/7 + 1;
	      $tmpdata[$j] = "$str,$tmpdata[$j]" if ($i % 7 eq 0);
      }
      else {
	      # $tmpdata[$d+1] .= ",$str";
	      $tmpdata[$d+1] = "$str," . $tmpdata[$d+1];
      }
      $i++;
      if ($first) {
	      my @z = ();
	      @z = split "," , $tmpdata[$d+1];
	      # $datecol = scalar @z;
	      $first = 0;
	      # print "@z\n"; print "$datecol\n"; exit;
      }
      $datecol = 1;
    }
    close DATE;
  }

  push @tmpdata, "EOF";
  push @data, @tmpdata;
  push @data, @tmpdata if $errorbars and $run == 0;
}

##########################################################

sub plot_command {
  my ($ptitle,$n,$run) = @_;

  # x-axis value
  my $x = "(\$2):";
  $x = "(\$0):" if $weeklycount;
  $x = "(\$0+get_shift($n)):" if $boxes;
  $x = "(\$0):" if $showdate;

  # y-axis value
  my $y = "\$7";		 #this is the median value (default)
  $y = "\$10" if $use_mean_std;	 # this is the mean
  $y = "\$" . ($run+11) if $run; # this is the individual run value

  # divide by pop size to normalize
  my $mult = 1.0;
  if ($normalize) {
    $mult = "(1.0*$scale/\$3)";
    $y = "$y*$mult";
  }

  # errorbars
  my $e = "(0)";
  if ($errorbars and $run == 0) {
    if ($use_mean_std) {
      $e = "(\$11*$mult)";
    } else {
      $e = "(\$6*$mult):(\$8*$mult)";
    }
  }

  # xtics
  my $xtic = "";
  $datecol = 2 if $weeklycount;
  if (($weeklycount or $showdate) and $n == 1) {
    $xtic = ":xtic(\$0>0 ? sprintf(date_format(int(\$0), strcol($datecol))) : '')";
  }
  
  # title
  my $title = "title '$ptitle'";
  
  # line color
  my $color_num = $ncolor % (scalar @colors);
  my $lt = "lt rgb '$colors[$color_num]'";
  if ($all) {
    if ($run == 0) {
      $lt = "lt -1";
    } else {
      $lt = "lt $run";
    }
  }
  $ncolor++;
  
  # line width
  my $lw = "lw 4";
  if ($pdfcairo) {
    $lw = "lw 2";
  }
  if ($linewidth) {
    $lw = "lw $linewidth";
  }
  if ($all and $run > 0) {
    if ($pdfcairo) {
      $lw = "lw 1";
    } else {
      $lw = "lw 2";
    }
  }
  $lw = "" if $boxes;

  # plot with lines?
  my $w = "with lines";
  $w = "with boxes" if $boxes;

  my $spec = "";
  $spec = "'-' using $x($y)$xtic $title $w $lt $lw, \\\n";
  $spec .= "'-' using $x($y):$e notitle wi errorbars lt -1 lw 2, \\\n" if $errorbars and $run == 0;
  return $spec;
}

sub get_runs {
  my $key = shift;
  my $id = $id_of_key{$key};
  my $runs = `cat $FREDRESULTS/JOB/$id/META/RUNS`;
  chomp $runs;
  return $runs;
}

